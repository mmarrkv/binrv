/*

Assumes subtraces already in place as per taint inference policy based on taint_vars set (prototype: execute xtract.py on main traces)

CC added note:
\x0A refers to newline
\|   matches subcalls in the trace
+    one or more matches
?    lazy match (non greedy)
*/



// Rule 1: newsession[Start] ->  server_connect[Normal] -> failed_cert_auth[Normal] -> abort[Accepting]
rule certerr_accept
{
    strings:
        $newsession = /SSL_ImportFD/          
        $server_connect =  /PR_Connect/
        /*                                              not newline or non-greedy new line etc... this matches the lines leading to err */
        $failed_cert_auth = /SSL_AuthCertificateComplete[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?err:0x[^0][0-9a-fA-F]+?\x0A/
        /*                ensures Destroy private key is a subcall of pr_close*/
        $abort = /PR_Close[^\x0A]+?\x0A[^\x0A]+?\x0A[^\|\x0A]+?\|[^\x0A]+?DestroyPrivateKey/

    condition:
        $newsession and $server_connect and $failed_cert_auth and $abort  and @newsession[1] < @server_connect[1] and @server_connect[1] < @failed_cert_auth[1] and @failed_cert_auth[1] < @abort[1] 
}


// Rule 2: newsession[Start] ->  server_connect[Normal] -> failed_cert_auth[Normal] -> not abort[Bad]
rule certerr_bad
{
    strings:
        $newsession = /SSL_ImportFD/          
        $server_connect =  /PR_Connect/
        $failed_cert_auth = /SSL_AuthCertificateComplete[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?err:0x[^0][0-9a-fA-F]+?\x0A/
        $abort = /PR_Close[^\x0A]+?\x0A[^\x0A]+?\x0A[^\|\x0A]+?\|[^\x0A]+?DestroyPrivateKey/

    condition:
        $newsession and $server_connect and $failed_cert_auth and not $abort and @newsession[1] < @server_connect[1] and @server_connect[1] < @failed_cert_auth[1]

}


// Rule 3: newsession[Start] ->  server_connect[Normal] -> (localecvalid -> remoteecvalid OR remoteecvalid -> localecvalid) -> not abort[Accepting]
rule masterkeyderive_accept
{
    strings:
        $newsession = /SSL_ImportFD/          
        $server_connect =  /PR_Connect/
        $localecvalid = /SECKEY_CreateECPrivateKey[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?EC_ValidatePublicKey[^\|]+?[\|][^\x0A]+?ret:0x0/
        $remoteecvalid = /K11_PubDeriveWithKDF[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?EC_ValidatePublicKey[^\|]+?[\|][^\x0A]+?ret:0x0/
        $abort = /PR_Close[^\x0A]+?\x0A[^\x0A]+?\x0A[^\|\x0A]+?\|[^\x0A]+?DestroyPrivateKey/

    condition:
        $newsession and $server_connect and $localecvalid and $remoteecvalid and not $abort and @newsession[1] < @server_connect[1] and @server_connect[1] < @localecvalid[#localecvalid] and @server_connect[1] < @remoteecvalid[#remoteecvalid] 
}


// Rule 4: newsession[Start] ->  server_connect[Normal] -> (localecinvalid OR remoteecinvalid)  -> not abort[Bad]
rule masterkeyderive_bad
{
    strings:
        $newsession = /SSL_ImportFD/          
        $server_connect =  /PR_Connect/
        $localecinvalid = /SECKEY_CreateECPrivateKey[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?EC_ValidatePublicKey[^\|]+?[\|][^\x0A]+?ret:0x[^0][0-9a-fA-F]+?\x0A/
        $remoteecinvalid = /K11_PubDeriveWithKDF[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?EC_ValidatePublicKey[^\|]+?[\|][^\x0A]+?ret:0x[^0][0-9a-fA-F]+?\x0A/
        $abort = /PR_Close[^\x0A]+?\x0A[^\x0A]+?\x0A[^\|\x0A]+?\|[^\x0A]+?DestroyPrivateKey/

    condition:
        $newsession and $server_connect and ($localecinvalid or $remoteecinvalid) and not $abort and @newsession[1] < @server_connect[1] and (@server_connect[1] < @localecinvalid[#localecinvalid] or @server_connect[1] < @remoteecinvalid[#remoteecinvalid]) 
}

// Rule 5: newsession[Start] ->  server_connect[Normal] -> master_session_key_derive[Normal] -> scrub_priv_key[Normal] -> END[Accepting]
rule scrubprivkey_accept
{
    strings:
        $newsession = /SSL_ImportFD/          
        $server_connect =  /PR_Connect/
        $localecvalid = /SECKEY_CreateECPrivateKey[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?EC_ValidatePublicKey[^\|]+?[\|][^\x0A]+?ret:0x0/
        $remoteecvalid = /K11_PubDeriveWithKDF[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?EC_ValidatePublicKey[^\|]+?[\|][^\x0A]+?ret:0x0/
        $scrub_priv_key = /SECKEY_DestroyPrivateKey[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?privk:[^\x0A]+?e5 e5 e5 e5/

    condition:
        $newsession and $server_connect and $localecvalid and $remoteecvalid and $scrub_priv_key and @newsession[1] < @server_connect[1] and @server_connect[1] < @localecvalid[#localecvalid] and @server_connect[1] < @remoteecvalid[#remoteecvalid] and @server_connect[1] < @scrub_priv_key[#scrub_priv_key]
}


// Rule 6: newsession[Start] ->  server_connect[Normal] -> master_session_key_derive[Normal] -> !scrub_priv_key -> END[Bad]
rule scrubprivkey_bad
{
    strings:
        $newsession = /SSL_ImportFD/          
        $server_connect =  /PR_Connect/
        $localecvalid = /SECKEY_CreateECPrivateKey[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?EC_ValidatePublicKey[^\|]+?[\|][^\x0A]+?ret:0x0/
        $remoteecvalid = /K11_PubDeriveWithKDF[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?EC_ValidatePublicKey[^\|]+?[\|][^\x0A]+?ret:0x0/
        $scrub_priv_key = /SECKEY_DestroyPrivateKey[^\x0A]+?\x0A[^\x0A]+?\x0A[^\x0A]+?privk:[^\x0A]+?e5 e5 e5 e5/

    condition:
        $newsession and $server_connect and $localecvalid and $remoteecvalid and (not $scrub_priv_key) and @newsession[1] < @server_connect[1] and @server_connect[1] < @localecvalid[#localecvalid] and @server_connect[1] < @remoteecvalid[#remoteecvalid] 
}


